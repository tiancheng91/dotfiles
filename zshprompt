#-*- mode: sh; -*-
autoload -Uz vcs_info
autoload colors zsh/terminfo

# My ZSH prompt theme to match the 88_madcows.theme for Irssi
# Author: Aaron Toponce
# License: Public Domain
# Modifications by: Dominik Honnef
function precmd {
    zstyle ':vcs_info:*' formats "${PR_BRIGHT_RED}[${PR_RESET} %s${PR_RED}@${PR_RESET}%b ${PR_BRIGHT_RED}]${PR_RESET}"
    zstyle ':vcs_info:*' actionformats "${PR_BRIGHT_RED}[${PR_RESET} %s${PR_RED}@${PR_RESET}%b${PR_RED}|${PR_RESET}%a ${PR_BRIGHT_RED}]${PR_RESET}"
    zstyle ':vcs_info:*' nvcsformats "${PR_BRIGHT_RED}[${PR_RESET} ∅ ${PR_BRIGHT_RED}]${PR_RESET}"
    vcs_info

    # The following 9 lines of code comes directly from Phil!'s ZSH prompt
    # http://aperiodic.net/phil/prompt/
    local TERMWIDTH
    (( TERMWIDTH = ${COLUMNS} - 1 ))

    local PROMPTSIZE=4
    local PWDSIZE=${#${(%):-%~}}

    if [[ "$PROMPTSIZE + $PWDSIZE" -gt $(($TERMWIDTH/2)) ]]; then
        (( PR_PWDLEN = $(($TERMWIDTH/2)) - $PROMPTSIZE ))
    fi

    # check if jobs are executing
    if [[ $(jobs | wc -l) -gt 0 ]]; then
        JOBS="${PR_RED}!${PR_RESET}J:%j"
    else
        JOBS=""
    fi

    if [[ -e ~/.rvm/bin/rvm-prompt ]] ;
    then
        RUBY=$(~/.rvm/bin/rvm-prompt)
    else
        RUBY=""
    fi

    if [[ -n "$RUBY" ]];
    then
        RUBY="${PR_BRIGHT_RED}[${PR_RESET} ${RUBY} ${PR_BRIGHT_RED}]${PR_RESET}";
    else
        RUBY="${PR_BRIGHT_RED}[${PR_RESET} ∅ ${PR_BRIGHT_RED}]${PR_RESET}";
    fi


# I want to know my battery percentage when running on battery power
    if which acpi &> /dev/null; then
        local BATTSTATE="$(acpi -b)"
        local BATTPRCNT="$(echo ${BATTSTATE[(w)4]}|sed -r 's/(^[0-9]+).*/\1/')"
        if [[ -z "${BATTPRCNT}" ]]; then
            PR_BATTERY=""
        elif [[ "${BATTPRCNT}" -lt 15 ]]; then
            PR_BATTERY="${PR_BOLD_RED} batt:${BATTPRCNT}%%"
        elif [[ "${BATTPRCNT}" -lt 50 ]]; then
            PR_BATTERY="${PR_BOLD_YELLOW} batt:${BATTPRCNT}%%"
        elif [[ "${BATTPRCNT}" -lt 100 ]]; then
            PR_BATTERY=" batt:${BATTPRCNT}%%"
        else
            PR_BATTERY=""
        fi
    fi
}

setprompt () {
    # Need this, so the prompt will work
    setopt prompt_subst

    # let's load colors into our environment, then set them
    autoload colors zsh/terminfo

    if [[ "$terminfo[colors]" -ge 8 ]]; then
        colors
    fi

    for COLOR in RED GREEN YELLOW WHITE BLACK; do
        eval PR_$COLOR='%{$fg[${(L)COLOR}]%}'
        eval PR_BRIGHT_$COLOR='%{$fg_bold[${(L)COLOR}]%}'
    done

    PR_RESET="%{$reset_color%}"

    # Finally, let's set the prompt

    PROMPT='$PR_SET_CHARSET$PR_STITLE${(e)PR_TITLEBAR}\
${PR_RESET}%n${PR_RED}@${PR_RESET}%m${JOBS}%(?..${PR_RED}!${PR_RESET}E:%?)\
${PR_BATTERY}${PR_WHITE} ${PR_BRIGHT_GREEN}%(!.%B#%b.\$)${PR_RESET} '

    # Of course we need a matching continuation prompt
    PROMPT2='\
${PR_BRIGHT_BLACK}>${PR_RESET}${PR_GREEN}>${PR_BRIGHT_GREEN}>\
${PR_RESET} %_ ${PR_BRIGHT_BLACK}>${PR_RESET}${PR_GREEN}>\
${PR_BRIGHT_GREEN}>${PR_WHITE} '

    RPROMPT='${PR_BRIGHT_RED}[${PR_RESET} \
${PR_RESET}%$PR_PWDLEN<...<%~%<< ${PR_BRIGHT_RED}]${PR_RESET} ${vcs_info_msg_0_} ${RUBY}'

    case $TERM in
        xterm*)
            PR_TITLEBAR=$'%{\e]0;%(!.-=*[ROOT]*=- | .)%n@%m:%~ | ${COLUMNS}x${LINES} | %y\a%}'
            ;;
        screen*)
            PR_TITLEBAR=$'%{\e_screen \005 (\005t) | %(!.-=[ROOT]=- | .)%n@%m:%~ | ${COLUMNS}x${LINES} | %y\e\\%}'
            ;;
        *)
            PR_TITLEBAR=''
            ;;
    esac
}


setprompt
